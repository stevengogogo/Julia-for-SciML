{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "---\n",
    "title: 'Example: Infectious Model'\n",
    "jupyter: 'julia-1.8'\n",
    "execute: \n",
    "  freeze: auto\n",
    "---"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "@raca"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 1,
   "metadata": {},
   "outputs": [
    {
     "name": "stderr",
     "output_type": "stream",
     "text": [
      "\u001b[32m\u001b[1m  Activating\u001b[22m\u001b[39m project at `~/Documents/GitHub/Julia-for-SciML/hands-on/tutorial`\n"
     ]
    }
   ],
   "source": [
    "using Pkg\n",
    "Pkg.activate(\"tutorial\")\n",
    "Pkg.instantiate()"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "cd(@__DIR__)\n",
    "using Pkg; Pkg.activate(\".\"); Pkg.instantiate()\n",
    "\n",
    "# Single experiment, move to ensemble further on\n",
    "# Some good parameter values are stored as comments right now\n",
    "# because this is really good practice\n",
    "\n",
    "using OrdinaryDiffEq\n",
    "using ModelingToolkit\n",
    "using DataDrivenDiffEq\n",
    "using LinearAlgebra, DiffEqSensitivity, Optim\n",
    "using DiffEqFlux, Flux\n",
    "using Plots\n",
    "gr()\n",
    "\n",
    "function corona!(du,u,p,t)\n",
    "    S,E,I,R,N,D,C = u\n",
    "    F, β0,α,κ,μ,σ,γ,d,λ = p\n",
    "    dS = -β0*S*F/N - β(t,β0,D,N,κ,α)*S*I/N -μ*S # susceptible\n",
    "    dE = β0*S*F/N + β(t,β0,D,N,κ,α)*S*I/N -(σ+μ)*E # exposed\n",
    "    dI = σ*E - (γ+μ)*I # infected\n",
    "    dR = γ*I - μ*R # removed (recovered + dead)\n",
    "    dN = -μ*N # total population\n",
    "    dD = d*γ*I - λ*D # severe, critical cases, and deaths\n",
    "    dC = σ*E # +cumulative cases\n",
    "\n",
    "    du[1] = dS; du[2] = dE; du[3] = dI; du[4] = dR\n",
    "    du[5] = dN; du[6] = dD; du[7] = dC\n",
    "end\n",
    "β(t,β0,D,N,κ,α) = β0*(1-α)*(1-D/N)^κ\n",
    "S0 = 14e6\n",
    "u0 = [0.9*S0, 0.0, 0.0, 0.0, S0, 0.0, 0.0]\n",
    "p_ = [10.0, 0.5944, 0.4239, 1117.3, 0.02, 1/3, 1/5,0.2, 1/11.2]\n",
    "R0 = p_[2]/p_[7]*p_[6]/(p_[6]+p_[5])\n",
    "tspan = (0.0, 21.0)\n",
    "prob = ODEProblem(corona!, u0, tspan, p_)\n",
    "solution = solve(prob, Vern7(), abstol=1e-12, reltol=1e-12, saveat = 1)\n",
    "\n",
    "tspan2 = (0.0,60.0)\n",
    "prob = ODEProblem(corona!, u0, tspan2, p_)\n",
    "solution_extrapolate = solve(prob, Vern7(), abstol=1e-12, reltol=1e-12, saveat = 1)\n",
    "\n",
    "# Ideal data\n",
    "tsdata = Array(solution)\n",
    "# Add noise to the data\n",
    "noisy_data = tsdata + Float32(1e-5)*randn(eltype(tsdata), size(tsdata))\n",
    "\n",
    "plot(abs.(tsdata-noisy_data)')\n",
    "\n",
    "### Neural ODE\n",
    "\n",
    "ann_node = FastChain(FastDense(7, 64, tanh),FastDense(64, 64, tanh), FastDense(64, 64, tanh), FastDense(64, 7))\n",
    "p = Float64.(initial_params(ann_node))\n",
    "\n",
    "function dudt_node(u,p,t)\n",
    "    S,E,I,R,N,D,C = u\n",
    "    F,β0,α,κ,μ,σ,γ,d,λ = p_\n",
    "    dS,dE,dI,dR,dD = ann_node([S/N,E,I,R,N,D/N,C],p)\n",
    "\n",
    "    dN = -μ*N # total population\n",
    "    dC = σ*E # +cumulative cases\n",
    "\n",
    "    [dS,dE,dI,dR,dN,dD,dC]\n",
    "end\n",
    "prob_node = ODEProblem(dudt_node, u0, tspan, p)\n",
    "s = concrete_solve(prob_node, Tsit5(), u0, p, saveat = solution.t)\n",
    "\n",
    "function predict(θ)\n",
    "    Array(concrete_solve(prob_node, Vern7(), u0, θ, saveat = 1,\n",
    "                         abstol=1e-6, reltol=1e-6,\n",
    "                         sensealg = InterpolatingAdjoint(autojacvec=ReverseDiffVJP())))\n",
    "end\n",
    "\n",
    "# No regularisation right now\n",
    "function loss(θ)\n",
    "    pred = predict(θ)\n",
    "    sum(abs2, (noisy_data[2:4,:] .- pred[2:4,:])), pred # + 1e-5*sum(sum.(abs, params(ann)))\n",
    "end\n",
    "\n",
    "loss(p)\n",
    "\n",
    "const losses = []\n",
    "callback(θ,l,pred) = begin\n",
    "    push!(losses, l)\n",
    "    if length(losses)%50==0\n",
    "        println(losses[end])\n",
    "    end\n",
    "    false\n",
    "end\n",
    "\n",
    "res1_node = DiffEqFlux.sciml_train(loss, p, ADAM(0.01), cb=callback, maxiters = 500)\n",
    "res2_node = DiffEqFlux.sciml_train(loss, res1_node.minimizer, BFGS(initial_stepnorm=0.01), cb=callback, maxiters = 10000)\n",
    "\n",
    "prob_node2 = ODEProblem(dudt_node, u0, tspan, res2_node.minimizer)\n",
    "s = solve(prob_node2, Tsit5(), saveat = 1)\n",
    "scatter(solution, vars=[2,3,4], label=[\"True Exposed\" \"True Infected\" \"True Recovered\"])\n",
    "plot!(s, vars=[2,3,4], label=[\"Estimated Exposed\" \"Estimated Infected\" \"Estimated Recovered\"])\n",
    "\n",
    "# Plot the losses\n",
    "plot(losses, yaxis = :log, xaxis = :log, xlabel = \"Iterations\", ylabel = \"Loss\")\n",
    "\n",
    "# Extrapolate out\n",
    "prob_node_extrapolate = ODEProblem(dudt_node,u0, tspan2, res2_node.minimizer)\n",
    "_sol_node = solve(prob_node_extrapolate, Vern7(), abstol=1e-12, reltol=1e-12, saveat = 1)\n",
    "p_node = scatter(solution_extrapolate, vars=[2,3,4], legend = :topleft, label=[\"True Exposed\" \"True Infected\" \"True Recovered\"], title=\"Neural ODE Extrapolation\")\n",
    "plot!(p_node,_sol_node, lw=5, vars=[2,3,4], label=[\"Estimated Exposed\" \"Estimated Infected\" \"Estimated Recovered\"])\n",
    "plot!(p_node,[20.99,21.01],[0.0,maximum(hcat(Array(solution_extrapolate[2:4,:]),Array(_sol_node[2:4,:])))],lw=5,color=:black,label=\"Training Data End\")\n",
    "\n",
    "savefig(\"neuralode_extrapolation.png\")\n",
    "savefig(\"neuralode_extrapolation.pdf\")\n",
    "\n",
    "### Universal ODE Part 1\n",
    "\n",
    "ann = FastChain(FastDense(3, 64, tanh),FastDense(64, 64, tanh), FastDense(64, 1))\n",
    "p = Float64.(initial_params(ann))\n",
    "\n",
    "function dudt_(u,p,t)\n",
    "    S,E,I,R,N,D,C = u\n",
    "    F, β0,α,κ,μ,σ,γ,d,λ = p_\n",
    "    z = ann([S/N,I,D/N],p) # Exposure does not depend on exposed, removed, or cumulative!\n",
    "    dS = -β0*S*F/N - z[1] -μ*S # susceptible\n",
    "    dE = β0*S*F/N + z[1] -(σ+μ)*E # exposed\n",
    "    dI = σ*E - (γ+μ)*I # infected\n",
    "    dR = γ*I - μ*R # removed (recovered + dead)\n",
    "    dN = -μ*N # total population\n",
    "    dD = d*γ*I - λ*D # severe, critical cases, and deaths\n",
    "    dC = σ*E # +cumulative cases\n",
    "\n",
    "    [dS,dE,dI,dR,dN,dD,dC]\n",
    "end\n",
    "prob_nn = ODEProblem(dudt_,u0, tspan, p)\n",
    "s = concrete_solve(prob_nn, Tsit5(), u0, p, saveat = 1)\n",
    "\n",
    "plot(solution, vars=[2,3,4])\n",
    "plot!(s[2:4,:]')\n",
    "\n",
    "function predict(θ)\n",
    "    Array(concrete_solve(prob_nn, Vern7(), u0, θ, saveat = solution.t,\n",
    "                         abstol=1e-6, reltol=1e-6,\n",
    "                         sensealg = InterpolatingAdjoint(autojacvec=ReverseDiffVJP())))\n",
    "end\n",
    "\n",
    "# No regularisation right now\n",
    "function loss(θ)\n",
    "    pred = predict(θ)\n",
    "    sum(abs2, noisy_data[2:4,:] .- pred[2:4,:]), pred # + 1e-5*sum(sum.(abs, params(ann)))\n",
    "end\n",
    "\n",
    "loss(p)\n",
    "\n",
    "const losses = []\n",
    "callback(θ,l,pred) = begin\n",
    "    push!(losses, l)\n",
    "    if length(losses)%50==0\n",
    "        println(losses[end])\n",
    "    end\n",
    "    false\n",
    "end\n",
    "\n",
    "res1_uode = DiffEqFlux.sciml_train(loss, p, ADAM(0.01), cb=callback, maxiters = 500)\n",
    "res2_uode = DiffEqFlux.sciml_train(loss, res1_uode.minimizer, BFGS(initial_stepnorm=0.01), cb=callback, maxiters = 10000)\n",
    "\n",
    "loss(res2_uode.minimizer)\n",
    "\n",
    "prob_nn2 = ODEProblem(dudt_,u0, tspan, res2_uode.minimizer)\n",
    "uode_sol = solve(prob_nn2, Tsit5(), saveat = 1)\n",
    "plot(solution, vars=[2,3,4])\n",
    "plot!(uode_sol, vars=[2,3,4])\n",
    "\n",
    "# Plot the losses\n",
    "plot(losses, yaxis = :log, xaxis = :log, xlabel = \"Iterations\", ylabel = \"Loss\")\n",
    "\n",
    "# Collect the state trajectory and the derivatives\n",
    "X = noisy_data\n",
    "# Ideal derivatives\n",
    "DX = Array(solution(solution.t, Val{1}))\n",
    "\n",
    "# Extrapolate out\n",
    "prob_nn2 = ODEProblem(dudt_,u0, tspan2, res2_uode.minimizer)\n",
    "_sol_uode = solve(prob_nn2, Vern7(), abstol=1e-12, reltol=1e-12, saveat = 1)\n",
    "p_uode = scatter(solution_extrapolate, vars=[2,3,4], legend = :topleft, label=[\"True Exposed\" \"True Infected\" \"True Recovered\"], title=\"Universal ODE Extrapolation\")\n",
    "plot!(p_uode,_sol_uode, lw = 5, vars=[2,3,4], label=[\"Estimated Exposed\" \"Estimated Infected\" \"Estimated Recovered\"])\n",
    "plot!(p_uode,[20.99,21.01],[0.0,maximum(hcat(Array(solution_extrapolate[2:4,:]),Array(_sol_uode[2:4,:])))],lw=5,color=:black,label=\"Training Data End\")\n",
    "\n",
    "savefig(\"universalode_extrapolation.png\")\n",
    "savefig(\"universalode_extrapolation.pdf\")\n",
    "\n",
    "### Universal ODE Part 2: SInDy to Equations\n",
    "\n",
    "# Create a Basis\n",
    "@variables u[1:3]\n",
    "# Lots of polynomials\n",
    "polys = Operation[]\n",
    "for i ∈ 0:2, j ∈ 0:2, k ∈ 0:2\n",
    "    push!(polys, u[1]^i * u[2]^j * u[3]^k)\n",
    "end\n",
    "\n",
    "# And some other stuff\n",
    "h = [cos.(u)...; sin.(u)...; unique(polys)...]\n",
    "basis = Basis(h, u)\n",
    "\n",
    "X = noisy_data\n",
    "# Ideal derivatives\n",
    "DX = Array(solution(solution.t, Val{1}))\n",
    "S,E,I,R,N,D,C = eachrow(X)\n",
    "F,β0,α,κ,μ,_,γ,d,λ = p_\n",
    "L = β.(0:tspan[end],β0,D,N,κ,α).*S.*I./N\n",
    "L̂ = vec(ann([S./N I D./N]',res2_uode.minimizer))\n",
    "X̂ = [S./N I D./N]'\n",
    "\n",
    "scatter(L,title=\"Estimated vs Expected Exposure Term\",label=\"True Exposure\")\n",
    "plot!(L̂,label=\"Estimated Exposure\")\n",
    "savefig(\"estimated_exposure.png\")\n",
    "savefig(\"estimated_exposure.pdf\")\n",
    "\n",
    "# Create an optimizer for the SINDY problem\n",
    "opt = SR3()\n",
    "# Create the thresholds which should be used in the search process\n",
    "thresholds = exp10.(-6:0.1:1)\n",
    "\n",
    "# Test on original data and without further knowledge\n",
    "Ψ_direct = SInDy(X[2:4, :], DX[2:4, :], basis, thresholds, opt = opt, maxiter = 50000) # Fail\n",
    "println(Ψ_direct.basis)\n",
    "# Test on ideal derivative data ( not available )\n",
    "Ψ_ideal = SInDy(X[2:4, 5:end], L[5:end], basis, thresholds, opt = opt, maxiter = 50000) # Succeed\n",
    "println(Ψ_ideal.basis)\n",
    "# Test on uode derivative data\n",
    "Ψ = SInDy(X̂[:, 2:end], L̂[2:end], basis, thresholds,  opt = opt, maxiter = 10000, normalize = true, denoise = true) # Succeed\n",
    "println(Ψ.basis)\n",
    "\n",
    "# Build a ODE for the estimated system\n",
    "function approx(u,p,t)\n",
    "    S,E,I,R,N,D,C = u\n",
    "    F, β0,α,κ,μ,σ,γ,d,λ = p_\n",
    "    z = Ψ([S/N,I,D/N]) # Exposure does not depend on exposed, removed, or cumulative!\n",
    "    dS = -β0*S*F/N - z[1] -μ*S # susceptible\n",
    "    dE = β0*S*F/N + z[1] -(σ+μ)*E # exposed\n",
    "    dI = σ*E - (γ+μ)*I # infected\n",
    "    dR = γ*I - μ*R # removed (recovered + dead)\n",
    "    dN = -μ*N # total population\n",
    "    dD = d*γ*I - λ*D # severe, critical cases, and deaths\n",
    "    dC = σ*E # +cumulative cases\n",
    "\n",
    "    [dS,dE,dI,dR,dN,dD,dC]\n",
    "end\n",
    "\n",
    "# Create the approximated problem and solution\n",
    "a_prob = ODEProblem{false}(approx, u0, tspan2, p_)\n",
    "a_solution = solve(a_prob, Tsit5())\n",
    "\n",
    "p_uodesindy = scatter(solution_extrapolate, vars=[2,3,4], legend = :topleft, label=[\"True Exposed\" \"True Infected\" \"True Recovered\"])\n",
    "plot!(p_uodesindy,a_solution, lw = 5, vars=[2,3,4], label=[\"Estimated Exposed\" \"Estimated Infected\" \"Estimated Recovered\"])\n",
    "plot!(p_uodesindy,[20.99,21.01],[0.0,maximum(hcat(Array(solution_extrapolate[2:4,:]),Array(_sol_uode[2:4,:])))],lw=5,color=:black,label=\"Training Data End\")\n",
    "\n",
    "savefig(\"universalodesindy_extrapolation.png\")\n",
    "savefig(\"universalodesindy_extrapolation.pdf\")"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.8.0",
   "language": "julia",
   "name": "julia-1.8"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.8.0"
  },
  "orig_nbformat": 4,
  "vscode": {
   "interpreter": {
    "hash": "aee8b7b246df8f9039afb4144a1f6fd8d2ca17a180786b69acc140d282b71a49"
   }
  }
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
